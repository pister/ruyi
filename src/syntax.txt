# Ruyi Program Language Syntax

# Programs
<root> ::= <package declaration>? <import declarations> <global declarations>?

# Declarations
<package declaration> ::= KW_PACKAGE <name> <statement ends>
<import declarations> ::= <import declaration>*
<import declaration> ::= KW_IMPORT <name> (IDENTITY)? <statement ends>
<global declarations> ::= (<global declaration> <statement ends>) *
<global declaration> ::= <variable declaration> | <function declaration> | <class declaration> | <interface declaration> | <constant declaration>
<variable declaration> ::= <variable auto infer type init> | (KW_VAR <variable declaration tail>)
<variable declaration tail> ::= IDENTITY <type>? (ASSIGN <expression>) ?
<variable auto infer type init> ::= IDENTITY COLON_ASSIGN <expression>
<function declaration> ::= KW_FUNC IDENTITY LPARAN <formal parameter list>? RPARAN <func return type>? <function body>
<class declaration> ::= KW_CLASS IDENTITY <class body>
<interface declaration> ::= KW_INTERFACE IDENTITY <interface body>
<constant declaration> ::= KW_CONST IDENTITY ASSIGN <constant initializer>
<class body> ::= LBRACE <class body declaration>* RBRACE
<class body declaration> ::= <class field declaration> | <class function declaration>
<class field declaration> ::= <variable declaration>
# member func as well as construtor but has same name with class, and has not return value
<class function declaration> ::= <function declaration>
<formal parameter list> ::= ( <formal parameter> ( COMMA <formal parameter>) * ) ?
<formal parameter> ::= IDENTITY DOT3? <type>
<function body> ::= <block>

# Blocks
<block> ::= LBRACE <block statements>? RBRACE
<block statements> ::= <block statement> *
<block statement> ::= ( <local variable declaration statement> | <statement> ) <statement ends>
<sub block statement> ::= LBRACE <block statements> RBRACE
<local variable declaration statement> ::= <local variable declaration>
<local variable declaration> ::= <variable declaration> | <variable auto infer type init>
<statement> ::= <labeled statement> | <if statement> | <while statement> | <expression statement>  | <for statement> | <switch statement> | <try statement> | <return statement> | <break statement> | <continue statement> | <sub block statement> 
<if statement> ::= KW_IF <expression> | ( LPARAN <expression> RPARAN ) <block> <elseif statement>* <else statement>?
<elseif statement> ::= KW_ELSEIF <expression> | ( LPARAN <expression> RPARAN ) <block>
<else statement> ::= KW_ELSE <block>
<while statement> ::= KW_WHILE <expression> | ( LPARAN <expression> RPARAN ) <block>
<return statement> ::= KW_RETURN ((<expression> (COMMA <expression>) *) | (LPARAN <expression> (COMMA <expression>) *) RPARAN)?
<for statement> ::= KW_FOR (<for in> | <for three parts>) | ( LPARAN <for in> | <for three parts> RPARAN) <block>
<for in> ::= IDENTITY (COMMA IDENTITY) * KW_IN <expression>
<for three parts> ::= <for init>? SEMICOLON <expression>? SEMICOLON <for update>?
<for init> ::= (<variable auto infer type init> (COMMA  <variable auto infer type init>) * ) ?
<for update> ::= (<statement expression> (COMMA <statement expression>) * ) ?
<switch statement> ::= KW_SWITCH <expression> | ( LPARAN <expression> RPARAN ) <switch statement body>
<switch statement body> ::= LBRACE <switch case statement>* <switch default statement>? RBRACE
<switch case statement> ::= KW_CASE <constant expression> (COMMA <constant expression>) * COLON <block statements>
<switch default statement> ::= KW_DEFAULT COLON <block statements>
<break statement> ::= KW_BREAK IDENTITY?
<continue statement> ::= KW_CONTINUE IDENTITY?
<labeled statement> ::= IDENTITY COLON <statement>
<expression statement> ::= <statement expression>
<statement expression> ::= <left hand side expression> | <instance creation expression>
<left hand side expression> ::= <left hand side> <left hand side expression tail>
<left hand side expression tail> ::= (ASSIGN <expression>) | (COLON_ASSIGN <expression>) | INC | DEC | <function invocation tail> 
<instance creation expression> ::= <map creation> | <array creation> | <instance creation>
<empty statement> ::= <statement ends>

#
<statement ends> ::= SEMICOLON *

# Expression
<constant expression> ::= <expression>
<expression> ::= <assignment expression>
<assignment expression> ::= <conditional expression> | <assignment>
<assignment> ::= <left hand side> <assignment operator> <assignment expression>
<left hand side> ::= <name> | <field access expression> | <array access>
<assignment operator> ::= ASSIGN | MUL_ASS | DIV_ASS | MOD_ASS | ADD_ASS | SUB_ASS | SHFT_LEFT_ASS | SHFT_RIGHT_ASS | BIT_AND_ASS | BIT_XOR_ASS | BIT_OR_ASS
<conditional expression> ::= <conditional or expression> (QM <expression> Ruyi_tt_COLON <conditional expression>)?
<conditional or expression> ::= <conditional and expression> (LOGIC_OR <conditional and expression>)*
<conditional and expression> ::= <bit or expression> ( LOGIC_AND <bit or expression>)*
<bit or expression> ::= <bit and expression> (BIT_OR <bit and expression>)*
<bit and expression> ::= <equality expression> (BIT_AND <equality expression>)*
<equality expression> ::= <relational expression> ((EQUALS | NOT_EQUALS) <relational expression>)?
<relational expression> ::= <shift expression> ((KW_INSTANCEOF <reference type>) | ((LT | GT | LTE | GTE) <shift expression>)) ?
<shift expression> ::= <additive expression> ((SHFT_LEFT | SHFT_RIGHT) <additive expression>)?
<additive expression> ::= <multiplicative expression> ((ADD | SUB) <multiplicative expression>)*
<multiplicative expression> ::= <unary expression> ((MUL | DIV | MOD) <unary expression>)*
<unary expression> ::= ADD <unary expression> | SUB <unary expression> | <not plus minus expression>
<not plus minus expression> ::= BIT_INVERSE <unary expression> | LOGIC_NOT <unary expression> | <postfix expression>
<postfix expression> ::= ( <field access expression> | <name> ) (DEC | INC | <type cast>) ?
<type cast> ::= DOT RPARAN <type> RPARAN
<field access expression> ::= <primary> (DOT <dot expression tail> | LBRACKET <bracket expression tail>) *
<dot expression tail> ::= IDENTITY (LPARAN <argument list>? RPARAN)?
<bracket expression tail> ::= <expression> RBRACKET
<field access expression> = (<field access expression> | <array access expression> ) <function invocation tail>?
<field access expression> ::= <primary> (DOT IDENTITY)*
<primary> ::= <array creation> | <map creation> | <anonymous function declaration> | <primary no new collection> 
<primary no new collection> ::= <literal> <function invocation tail>? | KW_THIS | LPARAN <expression> RPARAN | <instance creation>
<instance creation> ::= IDENTITY LBRACE (IDENTITY COLON <expression> (COMMA IDENTITY COLON <expression>)*)?  RBRACE
<argument list> ::= <expression> ( COMMA <expression> )*
<array creation> ::= <array creation with cap> | <array creation with init>
<array creation with cap> ::= KW_ARRAY LPARAN <array type> COMMA <expression> (COMMA <expression>)? RPARAN
<array creation with init> ::= <array type> LBRACKET (<expression> (COMMA <expression>)*)?  RBRACKET
<map creation> ::= KW_MAP LPARAN <map type> RPARAN
<function invocation tail> ::= LPARAN <argument list>? RPARAN
<array access> ::= <array variable access> | <variable primary access>
<array variable access> ::= <name> LBRACKET <expression> RBRACKET
<variable primary access> ::= <primary no new collection> LBRACKET <expression> RBRACKET
<anonymous function declaration> ::= KW_FUNC LPARAN <formal parameter list>? RPARAN <func return type>? <function body>


# Types
<type> ::= <primitive type> | <reference type>
<primitive type> ::= <numeric type> | bool
<numeric type> ::= <integral type> | <floating-point type>
<integral type> ::= KW_BYTE | KW_SHORT| KW_INT | KW_RUNE | KW_LONG
<floating-point type> ::= KW_FLOAT | KW_DOUBLE
<reference type> ::= IDENTITY | <array type> | <map type> | <func type>
<array type> ::= LBRACKET RBRACKET <type>
<map type> ::= LBRACKET IDENTITY RBRACKET <type>
<raw type> ::= <primitive type> | IDENTITY
<func type> ::= KW_FUNC LPARAN <parameter type list> RPARAN <func return type>?
<parameter type list> ::= ( <parameter type> ( COMMA <parameter type>) * ) ?
<parameter type> ::= DOT3? <type>
<func return type> ::= (<type> ( COMMA <type>) *) | (LPARAN <type> ( COMMA <type>) * RPARAN)


# Tokens
<name> ::= IDENTITY (DOT IDENTITY) *
<literal> ::= INTEGER | FLOAT | KW_TRUE | KW_FALSE | RUNE | STRING | KW_NULL | <name> 
